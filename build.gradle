plugins {
    id 'groovy'
    id 'java-library'
    id 'io.micronaut.application' version '4.5.4' apply false
    id 'io.micronaut.library' version '4.5.4' apply false
    id("io.micronaut.aot") version "4.5.4"
}

ext {
    // micronautVersion is defined in gradle.properties
    langchain4jVersion = '0.35.0'
    tikaVersion = '2.9.1'
    reactorVersion = '3.6.2'
}

allprojects {
    group = 'com.chatbotrag'
    version = '1.0.0'
    
    repositories {
        mavenCentral()
    }
}

// Common configuration for all modules
subprojects {
    apply plugin: 'groovy'
    apply plugin: 'java-library'
    
    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
    
    dependencies {
        implementation "org.slf4j:slf4j-api:2.0.12"
        implementation "org.apache.groovy:groovy:4.0.15"
        testImplementation "org.junit.jupiter:junit-jupiter:5.10.1"
    }
    
    test {
        useJUnitPlatform()
    }
}

// Chatbot API Application configuration
configure(project(':modules:chatbot-api')) {
    apply plugin: 'io.micronaut.application'
    apply plugin: 'application'
    
    micronaut {
        runtime 'netty'
        testRuntime 'junit5'
    }
    
    application {
        mainClass = 'com.chatbotrag.api.ChatbotRagApplication'
    }
}

// Frontend Application configuration
configure(project(':modules:chatbot-frontend')) {
    apply plugin: 'io.micronaut.application'
    apply plugin: 'application'
    
    micronaut {
        runtime 'netty'
        testRuntime 'spock2'
        processing {
            incremental(true)
            annotations("com.chatbotrag.*")
        }
    }
    
    application {
        mainClass = 'com.chatbotrag.frontend.FrontendApplication'
    }
}

// Document Processor CLI Application configuration
configure(project(':modules:document-processor')) {
    apply plugin: 'io.micronaut.application'

    micronaut {
        runtime 'netty'
        testRuntime 'spock2'
        processing {
            incremental(true)
            annotations("com.chatbotrag.processor.*")
        }
    }
    
    application {
        mainClass = 'com.chatbot.processor.CliCommand'
    }

    tasks.named("dockerfileNative") {
        jdkVersion = "21"
    }
}

// Library modules configuration
configure(subprojects.findAll { 
    it.name != 'chatbot-api' && 
    it.name != 'chatbot-frontend' && 
    it.name != 'document-processor' 
}) {
    apply plugin: 'io.micronaut.library'
}

// Custom Gradle Tasks
task buildAPI {
    description = 'Build API backend only'
    dependsOn 'clean', ':modules:chatbot-api:build'
}

task buildFrontend {
    description = 'Build frontend static assets'
    dependsOn 'clean', ':modules:chatbot-frontend:build'
}

task buildAll {
    description = 'Build both API and frontend'
    dependsOn 'buildAPI', 'buildFrontend'
}
